<html>

<head>
<title>AVT-WebGL</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<script type="text/javascript" src="js/gl-matrix-min.js"></script>
<script type="text/javascript" src="js/webgl-utils.js"></script>

<script id="shader-fs" type="x-shader/x-fragment">
    precision highp float;
    const int MaxLights = 1;

    struct Materials {
        vec4 diffuse;
        vec4 ambient;
        vec4 specular;
        vec4 emissive;
        float shininess;
        int texCount;
    };

    struct LightProperties {
        bool isEnabled;
        bool isLocal;           //true for a pointlight or a spotlight
        bool isSpot;
        vec4 l_pos;
        //spotlight attributes
        vec3 coneDirection;
        float spotCutOff;
        float spotExponent;
    };

    uniform LightProperties Lights[MaxLights];
    uniform bool darken; //if set to drue, darkens pixels a bit. used for reflections
    uniform Materials mat;
    uniform sampler2D texmap1;
    uniform sampler2D texmap2;
    uniform bool uUseLighting;
    uniform int depthFog;

    //esta info vem das normais interpoladas
    varying vec3 normal;
    varying vec3 eye;
    varying vec4 pos;
    varying vec2 vTextureCoord;

    const vec3 fogColor = vec3(0.5, 0.5,0.5);
    const float FogDensity = 0.025;

    void main() {
        int disabled = 0;
        vec4 spec = vec4(0.0);
        vec4 scatteredLight = vec4(0.0);
        vec4 reflectedLight = vec4(0.0);
        //estes vectores tem de ser normalizados pois foram interpolados
        vec3 n = normalize(normal);
        vec3 e = normalize(eye);

        //distance
        float dist = 0.0;
        float fogFactor = 0.0;
     
        //dist = abs(pos.z); //plane based
        dist = length(pos); //range based
     

        //loop over all the lights
        for (int light = 0; light < MaxLights; light++) {
            if (!Lights[light].isEnabled) 
            {   
                disabled++;
                continue;
            }

            float attenuation = 1.0;
            vec3 h;
            vec3 light_Dir = vec3(Lights[light].l_pos);

            if (Lights[light].isLocal) {
                light_Dir = light_Dir - vec3(pos);
                float light_Distance = length(light_Dir);
                light_Dir = light_Dir / light_Distance;
            
                attenuation = 1.0 / (light_Distance * light_Distance);
                
                if (Lights[light].isSpot) {
                    float spotCos = dot(light_Dir, -Lights[light].coneDirection);
                    if (spotCos < Lights[light].spotCutOff)
                        attenuation = 0.0;
                    else
                        attenuation *= pow(spotCos, Lights[light].spotExponent);
                }
            }

            else {
                //Directional Light
                light_Dir = vec3(Lights[light].l_pos);
            }

            h = normalize(light_Dir + e);
            float intDiff = max(dot(n, light_Dir), 0.0);
            float intSpec = max(dot(h,n), 0.0);

            if (intDiff == 0.0) 
                intSpec = 0.0;
            else 
                spec = mat.specular * pow(intSpec, mat.shininess);
            
            //Acumulate all the lights effect
            scatteredLight += mat.diffuse * intDiff * attenuation;
            reflectedLight += spec * attenuation;
            
        }
        vec4 texel, texel1, texel2;

        texel = texture2D(texmap1, vTextureCoord);
        if(texel.a == 0.0) discard;

        if (uUseLighting)
            gl_FragColor = max(mat.ambient*texel, scatteredLight*texel + reflectedLight);
            //gl_FragColor = texel;
        else if (!uUseLighting)
            gl_FragColor = texel;

        if (uUseLighting) {
            fogFactor = 1.0 /exp(dist * (dist/2.0) * FogDensity);
            fogFactor = clamp( fogFactor, 0.0, 1.0 );
     
            gl_FragColor = vec4(mix(fogColor, vec3(gl_FragColor), fogFactor), gl_FragColor.w);
        }

        if (darken) gl_FragColor *= vec4(1.3, 1.25, 1.2, 1);
    }
</script>

<script id="shader-vs" type="x-shader/x-vertex">

    uniform mat4 modelMatrix;
    uniform mat4 viewMatrix;
    uniform mat4 projectionMatrix;
    uniform mat3 uNMatrix;     //transposta da inversa da matriz viewModel

    attribute vec3 aVertexPosition;
    attribute vec3 aVertexNormal;    //por causa do gerador de geometria
    attribute vec2 aTextureCoord;

    varying vec3 normal;
    varying vec3 eye;
    varying vec4 pos;
    varying vec2 vTextureCoord;

    void main () {

        pos = viewMatrix * modelMatrix * vec4(aVertexPosition, 1.0);

        //tem de ser normalizado para as normais interpoladas terem a direccao correcta
        //mas as normais interpoladas podem nao ter tamanho 1
        //por isso voltam a ser normalizadas no fragment shader
        normal = normalize(uNMatrix * aVertexNormal); 
        vTextureCoord = aTextureCoord.st;
        
        eye = vec3(-pos);
        gl_Position = projectionMatrix * viewMatrix * modelMatrix * vec4(aVertexPosition, 1.0); //posicao do vertice em clip coordinates
    }
</script>

<script type="text/javascript" src="Assets/Models/Cube.js"></script>
<script type="text/javascript" src="Assets/Models/car_obj.js"></script>
<script type="text/javascript" src="Assets/Models/torus.js"></script>
<script type="text/javascript" src="Assets/Models/quad.js"></script>
<script type="text/javascript" src="Assets/Models/donut_obj.js"></script>
<script type="text/javascript" src="Assets/Models/burguer_obj.js"></script>
<script type="text/javascript" src="Assets/Models/road_obj.js"></script>
<script type="text/javascript" src="Assets/Models/iceCream_obj.js"></script>
<script type="text/javascript" src="Assets/Models/cake_obj.js"></script>
<script type="text/javascript" src="Assets/Models/orange_obj.js"></script>
<script type="text/javascript" src="Assets/Models/waffle_obj.js"></script>
<script type="text/javascript" src="Assets/Models/butter_obj.js"></script>
<script type="text/javascript" src="src/GameObject.js"></script>
<script type="text/javascript" src="src/Particles.js"></script>
<script type="text/javascript" src="src/main.js"></script>
<script type="text/javascript" src="src/Table.js"></script>
<script type="text/javascript" src="src/Road.js"></script>
<script type="text/javascript" src="src/Car.js"></script>
<script type="text/javascript" src="src/Donut.js"></script>
<script type="text/javascript" src="src/Burguer.js"></script>
<script type="text/javascript" src="src/IceCream.js"></script>
<script type="text/javascript" src="src/Cake.js"></script>
<script type="text/javascript" src="src/Waffle.js"></script>
<script type="text/javascript" src="src/Butter.js"></script>
<script type="text/javascript" src="src/Broccoli.js"></script>
<script type="text/javascript" src="src/Cup.js"></script>
<script type="text/javascript" src="src/Orange.js"></script>
<script type="text/javascript" src="src/Common.js"></script>

<body onload="webGLStart();">
<div class="container" style="position: relative;">
  <canvas id="AVT-WebGL" style="border: none; width:100%; height:97%; margin:0px; padding:0px; overflow:hidden"  position="absolute"></canvas>
  <div id="overlay" style="
      position: absolute;
      left: 10px;
      top: 10px;
      background-color: #FFFFFF;
      color: #000000;
      border: 4px solid #6D6D6D;
      padding: 9px;
      font-family: verdana;
      font-size: 21px;">
    <div>Score: <span id="score">0</span></div>
    <div> Lifes: <img src='Assets/Textures/carrito.png' style="width: 60px;height: 36px;"> </img> * <span id="lifes"> 5</span></div>
  </div>
</div>
</body>

</html>

</head>

</html>
